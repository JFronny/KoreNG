plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.10'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'org.beryx.jlink' version '2.24.1'
}

group 'gov.nsa.kore'
version '1.0'

repositories {
    mavenCentral()
    maven {
        name = "meteor-maven"
        url = "https://maven.meteordev.org"
    }
}

ext {
    junitVersion = '5.8.2'
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'gov.nsa.kore.ng'
    mainClass = 'gov.nsa.kore.ng.Main'
}

javafx {
    version = '17.0.0.1'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    implementation('org.controlsfx:controlsfx:11.1.0')
    implementation('org.kordamp.ikonli:ikonli-javafx:12.2.0')
    implementation('org.kordamp.ikonli:ikonli-fontawesome5-pack:12.2.0') // https://kordamp.org/ikonli/cheat-sheet-fontawesome5.html
    implementation "meteordevelopment:starscript:0.1.5"

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'KoreNG'
    }
}

tasks.jlink.doLast {
    copy {
        from("${projectDir}") {
            include "LICENSE", "KoreNG.bat", "KoreNG", "ai.kng"
        }
        into "${buildDir}/image"
    }
}